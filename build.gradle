/**
 * Gradle script to package Glassfish as a debian package.
 */
apply plugin: 'java'

ext.jdebVersion = '0.10'

ext.glassfishVersion = '3.1.2.2'

repositories {
  mavenCentral()
}

configurations {
  jdeb
}

dependencies {
  jdeb "org.vafer:jdeb:${jdebVersion}"
}

task unzip << {
  ant.unzip(src: "glassfish-${glassfishVersion}.zip", dest: "work")
  delete('work/glassfish3/.org.opensolaris,pkg')
  delete('work/glassfish3/glassfish/domains')
}

task debian() << {
  ant.taskdef(name: 'deb', classname: 'org.vafer.jdeb.ant.DebAntTask',
    classpath: configurations.jdeb.asPath)

  ant.deb(destfile: "build/glassfish3-${glassfishVersion}.deb",
      control: 'src/debian/config', verbose: 'true') {

    tarfileset(dir: 'src/debian/bin', prefix: '/etc/init.d', filemode: '755', username: 'glassfish', group: 'glassfish')
    tarfileset(dir: 'src/debian/etc', prefix: '/etc/default', includes: 'glassfish3', filemode: '600', username: 'glassfish', group: 'glassfish')
    tarfileset(dir: 'src/debian/etc', prefix: '/etc/glassfish3', includes: 'passfile.txt', filemode: '600', username: 'glassfish', group: 'glassfish')

    // finally include the whole Glassfish release
    tarfileset(dir: "work/glassfish3", prefix: "/usr/share/glassfish3", username: 'glassfish', group: 'glassfish')
  }

}
